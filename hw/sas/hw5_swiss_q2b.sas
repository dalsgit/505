options ls=78;
title "Confidence Intervals - Swiss Notes";
%let p=6;
data swiss;
  infile "x:\505\hw5_swiss.dat";
  input length leftWidth rightWidth bottomMargin topMargin diagonal;
  variable="length"; x=length; output;
  variable="leftWidth";    x=leftWidth;    output;
  variable="rightWidth"; x=rightWidth; output;
  variable="bottomMargin";   x=bottomMargin;       output;
  variable="topMargin";   x=topMargin;       output;
  variable="diagonal";   x=diagonal;       output;
  keep variable x;
  run;
proc sort;
  by variable;
  run;
proc means noprint;
  by variable;
  var x;
  output out=a n=n mean=xbar var=s2;
  run;
data b;
  set a;
  t1=tinv(1-0.025,n-1);
  tb=tinv(1-0.025/&p,n-1);
  f=finv(0.95,&p,n-&p);
  loone=xbar-t1*sqrt(s2/n);
  upone=xbar+t1*sqrt(s2/n);
  losim=xbar-sqrt(&p*(n-1)*f*s2/(n-&p)/n);
  upsim=xbar+sqrt(&p*(n-1)*f*s2/(n-&p)/n);
  lobon=xbar-tb*sqrt(s2/n);
  upbon=xbar+tb*sqrt(s2/n);
  run;
proc print;
  run;