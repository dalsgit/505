title "Skill data analysis";
data driver;
	infile 'x:\505\hw8_practice_skill.dat';
	input gender $ p1 p2 p3;
	y1=p2-p1;
	y2=p3-p2; 
	run;
proc glm data=driver;
	class gender;
	model p1 p2 p3 = gender;
	manova h=gender m=p3-p2,p2-p1;
	manova h=gender m=p1+p2+p3; 
	run;
data driver2;
	set driver;
	condition=1; k=p1; output;
	condition=2; k=p2; output;
	condition=3; k=p3; output;
	drop p1 p2 p3; 
	run;
proc sort;
	by gender condition; run;
proc means;
	by gender condition;
	var k;
	output out=a mean=mean; run;
proc gplot;
	axis1 length=4 in;
	axis2 length=5 in;
	plot mean*condition=gender / vaxis=axis1 haxis=axis2;
	symbol1 v=J f=special h=2 i=join color=black;
	symbol2 v=K f=special h=2 i=join color=black;
	symbol3 v=L f=special h=2 i=join color=black;
	symbol4 v=M f=special h=2 i=join color=black;
	run;
proc iml;
	start hotel;
		mu0={0, 0};
		one=j(nrow(x),1,1);
		ident=i(nrow(x));
		ybar=x`*one/nrow(x);
		s=x`*(ident-one*one`/nrow(x))*x/(nrow(x)-1.0);
		print mu0 ybar;
		print s;
		t2=nrow(x)*(ybar-mu0)`*inv(s)*(ybar-mu0);
		f=(nrow(x)-ncol(x))*t2/ncol(x)/(nrow(x)-1);
		df1=ncol(x);
		df2=nrow(x)-ncol(x);
		p=1-probf(f,df1,df2);
		print t2 f df1 df2 p;
	finish;
	
	use driver;
	read all var{y1 y2} into x;
	run hotel;
	quit;
	